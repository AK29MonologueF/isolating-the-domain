plugins {
    id 'java'
    id 'idea'
    id 'org.springframework.boot' version '2.3.3.RELEASE'
    id "org.dddjava.jig-gradle-plugin" version "2020.8.4"
}

[compileJava, processResources]*.shouldRunAfter(clean)
jigReports.dependsOn(clean, classes)

description 'ドメイン駆動設計によるアプリケーションテンプレート'

sourceCompatibility = '11'

sourceSets {
    main {
        // mybatis SQL map XML ファイルを java 以下でも検知する
        resources.srcDirs = ["src/main/java", "src/main/resources"]
    }
}

repositories {
    jcenter()
    maven {
        url "https://dl.bintray.com/jignite/maven/"
    }
}

dependencies {
    implementation platform('org.springframework.boot:spring-boot-dependencies:2.3.3.RELEASE')
    implementation("org.springframework.boot:spring-boot-starter-web")
    implementation("org.springframework.boot:spring-boot-starter-thymeleaf")
    implementation("org.springframework.boot:spring-boot-starter-validation")

    implementation("org.mybatis.spring.boot:mybatis-spring-boot-starter:2.1.3")

    runtimeOnly("com.h2database:h2")

    testImplementation("org.springframework.boot:spring-boot-starter-test") {
        exclude(group: 'org.junit.vintage')
    }
    testImplementation('org.seleniumhq.selenium:htmlunit-driver')
    testImplementation 'irof:jig-erd:release.latest'
}

test {
    useJUnitPlatform()
}

jig {
    linkPrefix = "https://github.com/system-sekkei/isolating-the-domain/tree/master/webapp/src/main/java"
}

